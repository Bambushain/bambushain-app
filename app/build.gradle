plugins {
    id 'com.android.application'
    id 'com.google.dagger.hilt.android'
}

def computeVersionCode() {
    def bambooReleaseVersion = (System.env.CI_PIPELINE_IID ?: '1') as Integer
    def bambooMajorVersion = bambooMajorVersion as Integer
    def bambooMinorVersion = bambooMinorVersion as Integer
    def versionCode = (bambooMajorVersion * 100000) + (bambooMinorVersion * 10000) + bambooReleaseVersion

    println "Versionname is $bambooMajorVersion.$bambooMinorVersion.$bambooReleaseVersion"
    println "Versioncode is $versionCode"

    return versionCode as Integer
}

android {
    namespace 'app.bambushain'
    compileSdk 34

    defaultConfig {
        applicationId "app.bambushain"
        minSdk 30
        targetSdk 34
        versionCode computeVersionCode()
        versionName "$bambooMajorVersion.$bambooMinorVersion"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    packagingOptions {
        exclude 'lib/armeabi-v7a/libsqlite3x.so'
        exclude 'lib/x86/libsqlite3x.so'
        exclude 'lib/x86_64/libsqlite3x.so'
    }

    signingConfigs {
        release {
            storeFile file(System.getenv('ANDROID_KEY_STOREFILE') ?: '/opt/secure/signing-key.jks')
            storePassword System.getenv('ANDROID_KEYSTORE_PASSWORD')
            keyAlias System.getenv('ANDROID_KEY_ALIAS')
            keyPassword System.getenv('ANDROID_KEYSTORE_PASSWORD')
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            debuggable false
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    buildFeatures {
        viewBinding true
        dataBinding true
    }
}

dependencies {
    implementation project(':api')
    implementation project(':models')

    implementation 'androidx.core:core:1.12.0'
    implementation 'androidx.activity:activity:1.8.2'
    implementation 'androidx.fragment:fragment:1.6.2'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.preference:preference:1.2.1'
    implementation "androidx.core:core-splashscreen:1.0.1"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.annotation:annotation:1.7.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.7.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.7.0'
    implementation "androidx.navigation:navigation-fragment:2.7.6"
    implementation "androidx.navigation:navigation-ui:2.7.6"
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'org.projectlombok:lombok:1.18.30'
    implementation 'com.google.dagger:hilt-android:2.50'
    implementation "io.reactivex.rxjava3:rxjava:3.1.8"
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.2'
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation 'com.squareup.retrofit2:adapter-rxjava3:2.9.0'
    implementation 'com.launchdarkly:okhttp-eventsource:4.1.1'
    implementation "androidx.room:room-runtime:2.6.1"
    implementation "androidx.room:room-rxjava3:2.6.1"
    implementation 'androidx.work:work-runtime:2.9.0'
    implementation 'com.github.requery:sqlite-android:3.43.0'

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'com.google.dagger:hilt-compiler:2.50'
    annotationProcessor 'androidx.databinding:databinding-compiler:8.2.1'
    annotationProcessor "androidx.room:room-compiler:2.6.1"

    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'
}
